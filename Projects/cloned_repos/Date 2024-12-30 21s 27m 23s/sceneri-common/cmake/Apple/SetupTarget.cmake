function(SetupAppleTarget target)
	get_target_property(target_type ${target} TYPE)
	if(target_type STREQUAL "EXECUTABLE")
		set_target_properties(${target} PROPERTIES
			RESOURCE "${RESOURCES}"
			XCODE_ATTRIBUTE_PRODUCT_BUNDLE_IDENTIFIER "com.${CMAKE_PROJECT_NAME}.${target}"
			XCODE_ATTRIBUTE_INFOPLIST_PREPROCESS YES
			XCODE_ATTRIBUTE_GCC_SYMBOLS_PRIVATE_EXTERN YES
			XCODE_ATTRIBUTE_STRIP_INSTALLED_PRODUCT NO
			XCODE_ATTRIBUTE_COPY_PHASE_STRIP NO
			MACOSX_BUNDLE_GUI_IDENTIFIER "com.${CMAKE_PROJECT_NAME}.${target}"
			MACOSX_BUNDLE_SHORT_VERSION_STRING "${CMAKE_PROJECT_VERSION_MAJOR}.${CMAKE_PROJECT_VERSION_MINOR}.${CMAKE_PROJECT_VERSION_PATCH}"
			MACOSX_BUNDLE_LONG_VERSION_STRING "${CMAKE_PROJECT_VERSION}"
			MACOSX_BUNDLE_BUNDLE_VERSION "${CMAKE_PROJECT_VERSION_MAJOR}.${CMAKE_PROJECT_VERSION_MINOR}.${CMAKE_PROJECT_VERSION_PATCH}"
			MACOSX_BUNDLE_BUNDLE_NAME "${CMAKE_PROJECT_NAME}"
			XCODE_ATTRIBUTE_INSTALL_PATH "$(LOCAL_APPS_DIR)"
			XCODE_ATTRIBUTE_SKIP_INSTALL "No"
			XCODE_ATTRIBUTE_COMBINE_HIDPI_IMAGES "No"
			XCODE_ATTRIBUTE_ASSETCATALOG_COMPILER_APPICON_NAME "AppIcon"
			XCODE_ATTRIBUTE_ASSETCATALOG_COMPILER_LAUNCHIMAGE_NAME "LaunchImage"
			XCODE_ATTRIBUTE_DEFINES_MODULE YES
			XCODE_ATTRIBUTE_PRODUCT_MODULE_NAME "${target}"
			XCODE_ATTRIBUTE_SWIFT_OBJC_INTEROP_MODE "objcxx"
		)

		set_target_properties(${target} PROPERTIES UNITY_BUILD OFF)

		if(PLATFORM_APPLE_MACCATALYST)
			set_target_properties(${target} PROPERTIES
				XCODE_ATTRIBUTE_SUPPORTS_MACCATALYST YES
			)
            set_target_properties(${target} PROPERTIES XCODE_ATTRIBUTE_TARGETED_DEVICE_FAMILY "6")
		endif()

        if(PLATFORM_APPLE_IOS)
            # <== 1 = iPhone, 2 = iPad
            set_target_properties(${target} PROPERTIES XCODE_ATTRIBUTE_TARGETED_DEVICE_FAMILY "1,2")
        endif()

        set_target_properties(${target} PROPERTIES XCODE_ATTRIBUTE_LD_RUNPATH_SEARCH_PATHS "@executable_path/Frameworks")
		set_target_properties(${target} PROPERTIES XCODE_ATTRIBUTE_OTHER_CODE_SIGN_FLAGS "--deep")

        if(PLATFORM_APPLE)
            target_link_libraries(${target} PRIVATE "-framework StoreKit")
            target_link_libraries(${target} PRIVATE "-framework AuthenticationServices")
            target_link_libraries(${target} PRIVATE "-framework UniformTypeIdentifiers")
        endif()

        if(PLATFORM_APPLE_MACOS)
            target_link_libraries(${target} PRIVATE "-framework AppKit")
        endif()
    endif()

    set_target_properties(${target} PROPERTIES
		XCODE_ATTRIBUTE_ONLY_ACTIVE_ARCH NO
		XCODE_ATTRIBUTE_ONLY_ACTIVE_ARCH[variant=Debug] "YES"
    )

	if(PLATFORM_APPLE_IOS)
        set_target_properties(${target} PROPERTIES
            XCODE_ATTRIBUTE_IPHONEOS_DEPLOYMENT_TARGET "15.2"
        )
	elseif(PLATFORM_APPLE_MACOS)
		set_target_properties(${target} PROPERTIES
            XCODE_ATTRIBUTE_MACOSX_DEPLOYMENT_TARGET "13.0"
        )
	elseif(PLATFORM_APPLE_VISIONOS)
        set_target_properties(${target} PROPERTIES
            XCODE_ATTRIBUTE_XROS_DEPLOYMENT_TARGET "2.0"
        )
    endif()

	# Stop Xcode from disabling warnings
	set_target_properties(${target} PROPERTIES
		XCODE_ATTRIBUTE_CLANG_WARN_IMPLICIT_SIGN_CONVERSION YES
		XCODE_ATTRIBUTE_CLANG_WARN_SUSPICIOUS_IMPLICIT_CONVERSION YES
		XCODE_ATTRIBUTE_CLANG_WARN__EXIT_TIME_DESTRUCTORS YES
		XCODE_ATTRIBUTE_GCC_INLINES_ARE_PRIVATE_EXTERN YES
		XCODE_ATTRIBUTE_GCC_STRICT_ALIASING YES
		XCODE_ATTRIBUTE_GCC_SYMBOLS_PRIVATE_EXTERN YES
		XCODE_ATTRIBUTE_GCC_TREAT_INCOMPATIBLE_POINTER_TYPE_WARNINGS_AS_ERRORS YES
		XCODE_ATTRIBUTE_GCC_WARN_64_TO_32_BIT_CONVERSION YES
		XCODE_ATTRIBUTE_GCC_WARN_ABOUT_MISSING_NEWLINE YES
		XCODE_ATTRIBUTE_GCC_WARN_ABOUT_RETURN_TYPE YES
		XCODE_ATTRIBUTE_GCC_WARN_FOUR_CHARACTER_CONSTANTS YES
		XCODE_ATTRIBUTE_GCC_WARN_HIDDEN_VIRTUAL_FUNCTIONS YES
		XCODE_ATTRIBUTE_GCC_WARN_INITIALIZER_NOT_FULLY_BRACKETED YES
		XCODE_ATTRIBUTE_GCC_WARN_NON_VIRTUAL_DESTRUCTOR NO
		XCODE_ATTRIBUTE_GCC_WARN_PEDANTIC YES
		XCODE_ATTRIBUTE_GCC_WARN_SHADOW YES
		XCODE_ATTRIBUTE_GCC_WARN_SIGN_COMPARE YES
		XCODE_ATTRIBUTE_GCC_WARN_UNINITIALIZED_AUTOS YES
		XCODE_ATTRIBUTE_GCC_WARN_UNUSED_PARAMETER YES
		XCODE_ATTRIBUTE_GCC_WARN_UNUSED_VARIABLE YES
		XCODE_ATTRIBUTE_GCC_WARN_UNUSED_VALUE YES
		XCODE_ATTRIBUTE_CLANG_WARN_EMPTY_BODY YES
		XCODE_ATTRIBUTE_CLANG_WARN_BOOL_CONVERSION YES
		XCODE_ATTRIBUTE_CLANG_WARN_CONSTANT_CONVERSION YES
		XCODE_ATTRIBUTE_CLANG_WARN_ENUM_CONVERSION YES
		XCODE_ATTRIBUTE_CLANG_WARN_INT_CONVERSION YES
		XCODE_ATTRIBUTE_CLANG_WARN_ASSIGN_ENUM YES
		XCODE_ATTRIBUTE_CLANG_WARN_UNREACHABLE_CODE YES
		XCODE_ATTRIBUTE_GCC_WARN_UNUSED_FUNCTION YES
		XCODE_ATTRIBUTE_GCC_WARN_UNUSED_LABEL YES
		XCODE_ATTRIBUTE_CLANG_WARN_CXX0X_EXTENSIONS YES
		XCODE_ATTRIBUTE_CLANG_WARN_DIRECT_OBJC_ISA_USAGE YES
		XCODE_ATTRIBUTE_CLANG_WARN_DUPLICATE_METHOD_MATCH YES
		XCODE_ATTRIBUTE_CLANG_WARN_OBJC_IMPLICIT_ATOMIC_PROPERTIES YES
		XCODE_ATTRIBUTE_GCC_WARN_UNDECLARED_SELECTOR YES
		XCODE_ATTRIBUTE_CLANG_WARN_OBJC_ROOT_CLASS YES
		XCODE_ATTRIBUTE_CLANG_WARN_OBJC_IMPLICIT_RETAIN_SELF YES
		XCODE_ATTRIBUTE_CLANG_WARN_OBJC_REPEATED_USE_OF_WEAK YES
		XCODE_ATTRIBUTE_CLANG_WARN_OBJC_RECEIVER_WEAK YES
		XCODE_ATTRIBUTE_GCC_WARN_CHECK_SWITCH_STATEMENTS YES
		XCODE_ATTRIBUTE_GCC_WARN_ABOUT_DEPRECATED_FUNCTIONS YES
		XCODE_ATTRIBUTE_GCC_WARN_MISSING_PARENTHESES YES
		XCODE_ATTRIBUTE_GCC_WARN_ABOUT_MISSING_FIELD_INITIALIZERS YES
		XCODE_ATTRIBUTE_GCC_WARN_ABOUT_MISSING_PROTOTYPES YES
		XCODE_ATTRIBUTE_GCC_WARN_ABOUT_POINTER_SIGNEDNESS YES
		XCODE_ATTRIBUTE_GCC_TREAT_IMPLICIT_FUNCTION_DECLARATIONS_AS_ERRORS YES
		XCODE_ATTRIBUTE_GCC_WARN_TYPECHECK_CALLS_TO_PRINTF YES
		XCODE_ATTRIBUTE_GCC_WARN_UNKNOWN_PRAGMAS YES
   	)

   	set_target_properties(${target} PROPERTIES
		XCODE_ATTRIBUTE_GCC_ENABLE_CPP_EXCEPTIONS NO
		XCODE_ATTRIBUTE_GCC_ENABLE_CPP_RTTI NO
		XCODE_ATTRIBUTE_GCC_ENABLE_EXCEPTIONS NO
		XCODE_ATTRIBUTE_CLANG_ENABLE_OBJC_ARC YES
	)

	target_compile_options(${target} PRIVATE "$<$<OR:$<COMPILE_LANGUAGE:CXX>,$<COMPILE_LANGUAGE:OBJCXX>>:-fobjc-arc>")
endfunction()
